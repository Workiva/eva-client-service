# EVA-Client-Service - EVA 101

<!-- toc -->

- [Transact Book Schema](#transact--book-schema)
- [Add First Book](#add-first-book)
- [Query for the First Book](#query-for-the-first-book)
- [Add Several Books](#add-several-books)
- [Find all books published in 2017](#find-all-books-published-in-2017)
- [Find author ID of "Designing Data-Intensive Applications"](#find-author-id-of-designing-data-intensive-applications)
- [Find author name of "Designing Data-Intensive Applications"](#find-author-name-of-designing-data-intensive-applications)
- [Find books from authored by "Steve McConnell"](#find-books-from-authored-by-steve-mcconnell)
- [Pull First Book By ID](#pull-first-book-by-id)
- [Query All Book Names](#query-all-book-names)
- [Determine when a book was transacted to the database](#determine-when-a-book-was-transacted-to-the-database)
- [Find titles and publish years for books published prior to 2005](#find-titles-and-publish-years-for-books-published-prior-to-2005)
- [Find books published in the years before than “Software Project Survival Guide”](#find-books-published-in-the-years-before-than-software-project-survival-guide)
- [Find the Oldest Book](#find-the-oldest-book)
- [Get Books with Rules](#get-books-with-rules)

<!-- tocstop -->

## Transact  Book Schema

### Description

The response of a successful transact is a map with four keys, `:db-before`, `:db-after`, `:tempids`, and `:tx-data`. `:db-before` and `:db-after` contains snapshots of the database before and after the transaction. `:tempids` contains a mapping of temporary ids that occurred as part of the transaction to their corresponding permanent ids. `:tx-data` contains the individual #datom vectors that were inserted into the database.


### Method - **POST**


### Headers

| Key | Value |
| --- | ----- |
| Accept | application/vnd.eva+edn |
| Content-Type | application/x-www-form-urlencoded |


### Body Fields - **urlencoded**

#### reference - _text_

```
#eva.client.service/connection-ref {
  :label "test-label"
}
```

#### transaction - _text_

```
[
  {:db/id #db/id [:db.part/user]
   :db/ident :book/title
   :db/doc "Title of a book"
   :db/valueType :db.type/string
   :db/cardinality :db.cardinality/one
   :db.install/_attribute :db.part/db}

  {:db/id #db/id [:db.part/user]
   :db/ident :book/year_published
   :db/doc "Date book was published"
   :db/valueType :db.type/long
   :db/cardinality :db.cardinality/one
   :db.install/_attribute :db.part/db}
  
  {:db/id #db/id [:db.part/user]
   :db/ident :book/author
   :db/doc "Author of a book"
   :db/valueType :db.type/ref
   :db/cardinality :db.cardinality/one
   :db.install/_attribute :db.part/db}
  
  {:db/id #db/id [:db.part/user]
   :db/ident :author/name
   :db/doc "Name of author"
   :db/valueType :db.type/string
   :db/cardinality :db.cardinality/one
   :db.install/_attribute :db.part/db}
]
```

## Add First Book

### Description

This variable will transact a single fact, which we call a datom, into the database. All datoms are made up of the 5-tuple, `[eid attr val tx added?]`. In this case, `eid` corresponds to `(eva/tempid :db.part/user)`, `attr` -> `:book/title`, and `val` -> `"First Book"`. `tx` and `added?` are filled in for us implicitly. `tx` refers to the id of the transaction entity that is created as part of every successful transaction and `added?` is simply a boolean value indicating whether this fact was added or retracted. A `datom` is the smallest unit of data that can be manipulated in the database.

`:db/add` is the keyword used to indicate an upsertion `(insert/update)` of data. Unlike our schema, `:db/id` is not required here as it is implicit when adding data in list form (more on that later). Instead of the db partition, which we used for our schema, we now use the `:db.part/user` partition.


### Method - **POST**


### Headers

| Key | Value |
| --- | ----- |
| Accept | application/vnd.eva+edn |
| Content-Type | application/x-www-form-urlencoded |


### Body Fields - **urlencoded**

#### reference - _text_

```
#eva.client.service/connection-ref {
  :label "test-label"
}
```

#### transaction - _text_

```
[
  [:db/add #db/id [:db.part/user] :book/title "First Book"]
  [:db/add #db/id [:db.part/tx] :author/name "Billy Baroo"]
]
```

## Query for the First Book

### Description

Next is the query, `[:find ?b :where [?b :book/title "First Book"]]`. There is a lot to take in here so we'll break it down piece-by-piece. First of all, every query you write needs to be wrapped in a vector `([...])`. The query starts with the `:find` keyword followed by a number of logic variables (`lvar` for short) denoted with a `?`. The `:where` clause follows and, similarly to SQL, is used to restrict the query results.

The tuple `[?b :book/title "First Book"]` is called a data pattern. All querying is essentially matching that pattern to the datom 5-tuple we discussed earlier (`[eid attr val tx added?]`). In this case we are asking for all of the entity ids (`?b`) which have the attribute `:book/title` with value `"First Book"`. What about `tx` and `added?`, why don't they appear in the clause? Simply, if not present they are replaced with implicit blanks. Expanding the tuple to its full form would yield, `[?b :book/title "First Book" _ _]`. We'll talk more about blanks later.


### Method - **POST**


### Headers

| Key | Value |
| --- | ----- |
| Accept | application/vnd.eva+edn |
| Content-Type | application/x-www-form-urlencoded |


### Body Fields - **urlencoded**

#### query - _text_

```
[:find ?b
 :in $ ?t
 :where [?b :book/title ?t]]
```

#### p[0] - _text_

```
#eva.client.service/snapshot-ref {
  :label "test-label"
}
```

#### p[1] - _text_

```
"First Book"
```

## Add Several Books

### Description

In the above, we define two vectors (dataset and dataset2) of maps containing data that we will transact into the database. One thing you might notice is that we omitted the `:db/add` keyword entirely. When you are transacting a single piece of data in a list, as we did above, you are limited to adding or retracting a specific fact about an entity. When transacting data in a map, the `:db/add` is implied and you can include any number of attribute/value pairs. Obviously the map form method is preferable when transacting larger amounts of data.

Another thing worth mentioning is that we are now passing a negative number as the second argument to all of our eva/tempid calls. Given the same partition and negative number, each invocation will return the same temporary id. The reason we do this is so that we can use that tempid to add references between entities in a single transaction. In this example we are using the tempid for the author entity as the reference value for the `:book/author` attribute.


### Method - **POST**


### Headers

| Key | Value |
| --- | ----- |
| Accept | application/vnd.eva+edn |
| Content-Type | application/x-www-form-urlencoded |


### Body Fields - **urlencoded**

#### reference - _text_

```
#eva.client.service/connection-ref {
  :label "test-label"
}
```

#### transaction - _text_

```
[
  {:db/id #db/id [:db.part/user -1]
   :author/name "Martin Kleppman"}
  {:db/id #db/id [:db.part/user -2]
   :book/title "Designing Data-Intensive Applications"
   :book/year_published 2017
   :book/author #db/id[:db.part/user -1]}

  {:db/id #db/id [:db.part/user -3]
   :author/name "Aurelien Geron"}
  {:db/id #db/id [:db.part/user -4]
   :book/title "Hands-On Machine Learning"
   :book/year_published 2017
   :book/author #db/id[ :db.part/user -3]}

  {:db/id #db/id [:db.part/user -5]
   :author/name "Wil van der Aalst"}
  {:db/id #db/id [:db.part/user -6]
   :book/title "Process Mining: Data Science in Action"
   :book/year_published 2016
   :book/author #db/id[ :db.part/user -5]}
  {:db/id #db/id [:db.part/user -7]
   :book/title "Modeling Business Processes: A Petri-Net Oriented Approach"
   :book/year_published 2011
   :book/author #db/id[ :db.part/user -5]}

  {:db/id #db/id [:db.part/user -8]
   :author/name "Edward Tufte"}
  {:db/id #db/id [:db.part/user -9]
   :book/title "The Visual Display of Quantitative Information"
   :book/year_published 2001
   :book/author #db/id[ :db.part/user -8]}
  {:db/id #db/id [:db.part/user -10]
   :book/title "Envisioning Information"
   :book/year_published 1990
   :book/author #db/id[ :db.part/user -8]}

  {:db/id #db/id [:db.part/user -11]
   :author/name "Ramez Elmasri"}
  {:db/id #db/id [:db.part/user -12]
   :book/title "Operating Systems: A Spiral Approach"
   :book/year_published 2009
   :book/author #db/id[ :db.part/user -11]}
  {:db/id #db/id [:db.part/user -13]
   :book/title "Fundamentals of Database Systems"
   :book/year_published 2006
   :book/author #db/id[ :db.part/user -11]}

  {:db/id #db/id [:db.part/user -14]
   :author/name "Steve McConnell"}
  {:db/id #db/id [:db.part/user -15]
   :book/title "Code Complete: A Practical Handbook of Software Construction"
   :book/year_published 2004
   :book/author #db/id[:db.part/user -14]}
  {:db/id #db/id [:db.part/user -16]
   :book/title "Software Estimation: Demystifying the Black Art"
   :book/year_published 2006
   :book/author #db/id[ :db.part/user -14]}
  {:db/id #db/id [:db.part/user -17]
   :book/title "Rapid Development: Taming Wild Software Schedules"
   :book/year_published 1996
   :book/author #db/id[:db.part/user -14]}
  {:db/id #db/id [:db.part/user -18]
   :book/title "Software Project Survival Guide"
   :book/year_published 1997
   :book/author #db/id[ :db.part/user -14]}
  {:db/id #db/id [:db.part/user -19]
   :book/title "After the Gold Rush: Creating a True Profession of Software Engineering"
   :book/year_published 1999
   :book/author #db/id[ :db.part/user -14]}
  
  {:db/id #db/id [:db.part/user -20]
   :author/name "Don Miguel Ruiz"}
  {:db/id #db/id [:db.part/user -21]
   :book/title "The Four Agreements: A Practical Guide to Personal Freedom"
   :book/year_published 2011
   :book/author #db/id[ :db.part/user -20]}
  
  {:db/id #db/id [:db.part/user -22]
   :author/name "Charles Petzold"}
  {:db/id #db/id [:db.part/user -23]
   :book/title "Code: The Hidden Language of Computer Hardware and Software"
   :book/year_published 2000
   :book/author #db/id[ :db.part/user -22]}
  
  {:db/id #db/id [:db.part/user -24]
   :author/name "Anil Maheshwari"}
  {:db/id #db/id [:db.part/user -25]
   :book/title "Data Analytics Made Accessible"
   :book/year_published 2014
   :book/author #db/id[ :db.part/user -24]}

  {:db/id #db/id [:db.part/user -26]
   :author/name "Jeremy Anderson"}
  {:db/id #db/id [:db.part/user -27]
   :book/title "Professional Clojure"
   :book/year_published 2016
   :book/author #db/id[:db.part/user -26]}
]
```

## Find all books published in 2017

### Description

In this example, we use a logic variable `?title` to find the title of books released in `1990`. One interesting thing to note about this query is that `?b` is used twice. When a logic variable is used more than once it must represent the same entity in every clause in order to satisfy the set of clauses. This is also referred to as unification. In SQL, this is roughly equivalent to joining the years and titles on the shared entity ids.


### Method - **POST**


### Headers

| Key | Value |
| --- | ----- |
| Accept | application/vnd.eva+edn |
| Content-Type | application/x-www-form-urlencoded |


### Body Fields - **urlencoded**

#### query - _text_

```
[:find ?title
 :where [?b :book/year_published 2017]
        [?b :book/title ?title]]
```

#### p[0] - _text_

```
#eva.client.service/snapshot-ref {
  :label "test-label"
}
```

## Find author ID of "Designing Data-Intensive Applications"

### Description

In this query we are following a relationship from a book to its author, and then finding and returning the entity ID of the author. We bind the logic variable `?a` to the entity id of the author associated with the book.


### Method - **POST**


### Headers

| Key | Value |
| --- | ----- |
| Accept | application/vnd.eva+edn |
| Content-Type | application/x-www-form-urlencoded |


### Body Fields - **urlencoded**

#### query - _text_

```
[:find ?a
 :where [?b :book/title "Designing Data-Intensive Applications"]
        [?b :book/author ?a]]
```

#### p[0] - _text_

```
#eva.client.service/snapshot-ref {
  :label "test-label"
} 
```

## Find author name of "Designing Data-Intensive Applications"

### Description

In this query we are following a relationship from a book to its author, and then finding and returning the name of the author. We bind the logic variable `?a` to the entity id of the author associated with the book. Then we use that entity id to get the name of the actual author.


### Method - **POST**


### Headers

| Key | Value |
| --- | ----- |
| Accept | application/vnd.eva+edn |
| Content-Type | application/x-www-form-urlencoded |


### Body Fields - **urlencoded**

#### query - _text_

```
[:find ?name
 :where [?b :book/title "Designing Data-Intensive Applications"]
        [?b :book/author ?a]
        [?a :author/name ?name]]
```

#### p[0] - _text_

```
#eva.client.service/snapshot-ref {
  :label "test-label"
} 
```

## Find books from authored by "Steve McConnell"

### Description

We can just as easily reverse the previous query and get all of the books published by a certain author.


### Method - **POST**


### Headers

| Key | Value |
| --- | ----- |
| Accept | application/vnd.eva+edn |
| Content-Type | application/x-www-form-urlencoded |


### Body Fields - **urlencoded**

#### query - _text_

```
[:find ?books
 :where [?b :book/title ?books]
        [?b :book/author ?a]
        [?a :author/name "Steve McConnell"]]
```

#### p[0] - _text_

```
#eva.client.service/snapshot-ref {
  :label "test-label"
} 
```

## Pull First Book By ID

### Description

Up to this point we have been querying data by unifying individual values after our `:find` clause to those in our `:where` clause. Using the Pull API, we can make the following call (replace eid with the result of our very first query (Add First Book).

The Pull API expects a db as its first argument, similar to how we pass db to our queries. The second argument `[*]` is a pattern, where you can specify which attributes you would like to have returned. Using `*` as your pattern indicates you want all of the attributes for this particular entity. The final argument is the id of the entity you are trying to get.


### Method - **POST**


### Headers

| Key | Value |
| --- | ----- |
| Accept | application/vnd.eva+edn |
| Content-Type | application/x-www-form-urlencoded |


### Body Fields - **urlencoded**

#### reference - _text_

```
#eva.client.service/snapshot-ref {
  :label "test-label"
} 
```

#### ids - _text_

```
8796093023236
```

#### pattern - _text_

```
[*]
```

## Query All Book Names

### Description

Instead of using a logic variable to bind the id of a book entity we simply use `_`. The underscore is equivalent to a wildcard and will match anything, so every entity id for this attribute will be returned by the query.


### Method - **POST**


### Headers

| Key | Value |
| --- | ----- |
| Accept | application/vnd.eva+edn |
| Content-Type | application/x-www-form-urlencoded |


### Body Fields - **urlencoded**

#### query - _text_

```
[:find ?name
 :where [_ :book/title ?name]]
```

#### p[0] - _text_

```
#eva.client.service/snapshot-ref {
  :label "test-label"
} 
```

## Determine when a book was transacted to the database

### Description

Whenever you transact data into Eva, a transaction entity is created as well. The transaction entity contains the instant (timestamp) a transaction is committed, which is useful to know in some circumstances.

It is an important realization that `:db` keywords can be queried the same way as user-defined schema. Another thing in the above example that may look weird is that we have four arguments in one of our datom clauses where previously we've only used three. Remember that the datom is a 5-tuple `[eid attr val tx added?]`. In this case `?tx` binds to the tx portion of the datom, which is what we are trying to query for.


### Method - **POST**


### Headers

| Key | Value |
| --- | ----- |
| Accept | application/vnd.eva+edn |
| Content-Type | application/x-www-form-urlencoded |


### Body Fields - **urlencoded**

#### query - _text_

```
[:find ?timestamp
 :where [_ :book/title "Process Mining: Data Science in Action" ?tx]
        [?tx :db/txInstant ?timestamp]]
```

#### p[0] - _text_

```
#eva.client.service/snapshot-ref {
  :label "test-label"
} 
```

## Find titles and publish years for books published prior to 2005

### Description

The `(< ?year 2005)` clause is called a predicate, and filters the result set to only include the results which satisfy the predicate. Any Clojure function or Java method can be used as a predicate.


### Method - **POST**


### Headers

| Key | Value |
| --- | ----- |
| Accept | application/vnd.eva+edn |
| Content-Type | application/x-www-form-urlencoded |


### Body Fields - **urlencoded**

#### query - _text_

```
[:find ?book ?year
 :where [?b :book/title ?book]
        [?b :book/year_published ?year]
        [(< ?year 2005)]]
```

#### p[0] - _text_

```
#eva.client.service/snapshot-ref {
  :label "test-label"
} 
```

## Find books published in the years before than “Software Project Survival Guide”

### Description

Another usage of predicate functions.


### Method - **POST**


### Headers

| Key | Value |
| --- | ----- |
| Accept | application/vnd.eva+edn |
| Content-Type | application/x-www-form-urlencoded |


### Body Fields - **urlencoded**

#### query - _text_

```
[:find ?book ?y1
 :where [?b1 :book/title ?book]
        [?b1 :book/year_published ?y1]
        [?b2 :book/title "Software Project Survival Guide"]
        [?b2 :book/year_published ?y2]
        [(< ?y1 ?y2)]]
```

#### p[0] - _text_

```
#eva.client.service/snapshot-ref {
  :label "test-label"
} 
```

## Find the Oldest Book

### Description

What if we want to find the oldest or the newest book? Datalog supports a number of aggregate functions, including `min`, `max`, `avg` and `sum`.


### Method - **POST**


### Headers

| Key | Value |
| --- | ----- |
| Accept | application/vnd.eva+edn |
| Content-Type | application/x-www-form-urlencoded |


### Body Fields - **urlencoded**

#### query - _text_

```
[:find (min ?year)
 :where [_ :book/year_published ?year]]
```

#### p[0] - _text_

```
#eva.client.service/snapshot-ref {
  :label "test-label"
} 
```

## Get Books with Rules

### Description

Throughout this tutorial, if we wanted to the get the author for a particular book we'd need to write the same two where clauses every time. This gets old really fast, and can get quite tedious for more complicated queries. Rules offer a way to abstract away reusable components of a query.

We've created a rule in this example called book-author. The part contained within the first vector `(...)` is called the head of the rule, containing the name and any logic variables. The rest of the rule looks exactly like the where clauses we've seen up to this point. The `?book` and `?name` variables can be used for both input and output. For example, if you provide a value for `?book` the output will be the author of that book. Vice versa, if you provide a value for `?name` you will get back the titles of all the books for that author. If you provide a value for neither `?book` or `?name` the query will return all the possible combinations in the database. To use a rule in a query firstly we need to pass the rule into the `:in` clause using the `%` symbol. Secondly we call the rule in one of our `:where` clauses like `(book-author "Modeling Business Processes: A Petri-Net Oriented Approach" ?name)`.


### Method - **POST**


### Headers

| Key | Value |
| --- | ----- |
| Accept | application/vnd.eva+edn |
| Content-Type | application/x-www-form-urlencoded |


### Body Fields - **urlencoded**

#### query - _text_

```
[:find ?name
 :in $ %
 :where (book-author "Modeling Business Processes: A Petri-Net Oriented Approach" ?name)]
```

#### p[1] - _text_

```
[[(book-author ?book ?name)
 [?b :book/title ?book]
 [?b :book/author ?a]
 [?a :author/name ?name]]]
```

#### p[0] - _text_

```
#eva.client.service/snapshot-ref {
  :label "test-label"
} 
```

